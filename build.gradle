plugins {
	id 'io.github.ladysnake.chenille' version '0.6.0'
	id 'org.quiltmc.quilt-mappings-on-loom' version '3.1.1'
	id 'io.github.juuxel.loom-quiltflower' version '1.6.0'
}

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

chenille {
	configurePublishing {
		withArtifactory()
		withGithubRelease()
	}
	configureTestmod {
		withDependencyConfiguration()
	}

	javaVersion = 17
	license = 'LGPL'
}

sourceSets {
	dummy
}

repositories {
	chenille.repositories {
		ladysnake()
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modLocalImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}")
	modLocalImplementation("dev.onyxstudios.cardinal-components-api:cardinal-components-base:${cca_version}")
	modLocalImplementation("dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${cca_version}")
	api 'junit:junit:4.13'
	testmodImplementation sourceSets.main.output
	annotationProcessor(sourceSets.dummy.output)
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}
